<!DOCTYPE html>
<html>
    <head>
        <title>PaintFlow</title>
        <link rel="stylesheet" type="text/css" href="../css/paintflow.css"/>
        <script type="text/javascript" src="../3rd/three.js"></script>
        <script type="text/javascript" src="../3rd/dat.gui.js"></script>
        <script type="text/javascript" src="../3rd/jquery-1.11.2.min.js"></script>
        <script type="text/javascript" src="paintflow.js"></script>
        <script type="x-shader/x-vertex" id="standardVertexShader">
            varying vec2 vUv;

            void main()
            {
                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="simplexNoiseFragmentShader">
            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform float scalef;
            uniform float sofff;
            uniform vec2 brush;
            uniform float brushsize;
            uniform sampler2D tSource;

            //
            // Description : Array and textureless GLSL 2D simplex noise function.
            //      Author : Ian McEwan, Ashima Arts.
            //  Maintainer : ijm
            //     Lastmod : 20110822 (ijm)
            //     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
            //               Distributed under the MIT License. See LICENSE file.
            //               https://github.com/ashima/webgl-noise
            //

            vec3 mod289(vec3 x) {
                return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec2 mod289(vec2 x) {
                return x - floor(x * (1.0 / 289.0)) * 289.0;
            }

            vec3 permute(vec3 x) {
                return mod289(((x*34.0)+1.0)*x);
            }

            float snoise(vec2 v) {
                const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                                    0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                                   -0.577350269189626,  // -1.0 + 2.0 * C.x
                                    0.024390243902439); // 1.0 / 41.0
                // First corner
                vec2 i  = floor(v + dot(v, C.yy) );
                vec2 x0 = v -   i + dot(i, C.xx);

                // Other corners
                vec2 i1;
                //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
                //i1.y = 1.0 - i1.x;
                i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
                // x0 = x0 - 0.0 + 0.0 * C.xx ;
                // x1 = x0 - i1 + 1.0 * C.xx ;
                // x2 = x0 - 1.0 + 2.0 * C.xx ;
                vec4 x12 = x0.xyxy + C.xxzz;
                x12.xy -= i1;

                // Permutations
                i = mod289(i); // Avoid truncation effects in permutation
                vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));

                vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
                m = m*m ;
                m = m*m ;

                // Gradients: 41 points uniformly over a line, mapped onto a diamond.
                // The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)

                vec3 x = 2.0 * fract(p * C.www) - 1.0;
                vec3 h = abs(x) - 0.5;
                vec3 ox = floor(x + 0.5);
                vec3 a0 = x - ox;

                // Normalise gradients implicitly by scaling m
                // Approximation of: m *= inversesqrt( a0*a0 + h*h );
                m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );

                // Compute final noise value at P
                vec3 g;
                g.x  = a0.x  * x0.x  + h.x  * x0.y;
                g.yz = a0.yz * x12.xz + h.yz * x12.yw;
               //return 130.0 * dot(m, g);
               return dot(m, g);
            }

            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;
            vec2 texel = vec2(step_x, step_y);

            void main() {
                float a, b = 0.0;
                vec2 offset = vec2(sofff, sofff);
                vec2 scale = scalef * vec2(1.0, screenHeight/screenWidth);
                a = snoise(scale * vUv.xy);
                b = snoise(scale * (vUv.xy + offset));
                gl_FragColor = vec4(a, b, 0.0, 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="smokeFluidFragmentShader">
            const float _K0 = -20.0/6.0; // center weight
            const float _K1 = 4.0/6.0; // edge-neighbors
            const float _K2 = 1.0/6.0; // vertex-neighbors
            const float sq2 = 0.7071068; // sqrt(2.0)/2.0;
            const float wds = 2.0 + 4.0 * sq2;
            const float pi_over_2 = 1.57079632679;

            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform sampler2D tSource;
            uniform float curlf;
            uniform float fluxf;
            uniform float divf;
            uniform float lapf;
            uniform float feedf;
            uniform float expf;
            uniform vec2 brush;
            uniform float brushsize;

            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;
            vec2 texel = vec2(step_x, step_y);

            vec2 n  = vec2(0.0, step_y);
            vec2 ne = vec2(step_x, step_y);
            vec2 e  = vec2(step_x, 0.0);
            vec2 se = vec2(step_x, -step_y);
            vec2 s  = vec2(0.0, -step_y);
            vec2 sw = vec2(-step_x, -step_y);
            vec2 w  = vec2(-step_x, 0.0);
            vec2 nw = vec2(-step_x, step_y);

            float atan2(in float y, in float x)
            {
                float s = (abs(x) > abs(y)) ? 1.0 : 0.0;
                return mix(pi_over_2 - atan(x,y), atan(y,x), s);
            }

            void main()
            {
                vec3 uv = texture2D(tSource, vUv).rgb;

                vec3 uv_n = texture2D(tSource, vUv+n).rgb;
                vec3 uv_e = texture2D(tSource, vUv+e).rgb;
                vec3 uv_s = texture2D(tSource, vUv+s).rgb;
                vec3 uv_w = texture2D(tSource, vUv+w).rgb;

                vec3 uv_nw = texture2D(tSource, vUv+nw).rgb;
                vec3 uv_sw = texture2D(tSource, vUv+sw).rgb;
                vec3 uv_ne = texture2D(tSource, vUv+ne).rgb;
                vec3 uv_se = texture2D(tSource, vUv+se).rgb;

                vec3 lapl  = _K0*uv + _K1*(uv_n + uv_e + uv_w + uv_s) + _K2*(uv_nw + uv_sw + uv_ne + uv_se);
                float curl = (uv_n.r - uv_s.r - uv_e.g + uv_w.g + sq2 * (uv_nw.r + uv_nw.g) + sq2 * (uv_ne.r - uv_ne.g) + sq2 * (uv_sw.g - uv_sw.r) - sq2 * (uv_se.g + uv_se.r));
                float curl_e = curlf * sign(curl) * pow(abs(curl), expf);
                vec2 flux  = vec2(uv_w.r + uv_e.r - wds * uv.r, uv_s.g + uv_n.g - wds * uv.g) + sq2 * (uv_nw.rg + uv_ne.rg + uv_se.rg + uv_sw.rg);
                float div  = uv_s.g - uv_n.g - uv_e.r + uv_w.r + sq2 * (uv_nw.r - uv_nw.g) - sq2 * (uv_ne.r + uv_ne.g) + sq2 * (uv_sw.r + uv_sw.g) + sq2 * (uv_se.g - uv_se.r);

                float theta, mag, cost, sint = 0.0;
                if (!(uv.r == 0.0 && uv.g == 0.0)) {
                    theta = atan2(uv.g, uv.r);
                    mag = sqrt(uv.r*uv.r + uv.g*uv.g);
                    cost = cos(theta);
                    sint = sin(theta);
                }

                float ta = feedf * uv.r + fluxf * flux.r - lapf * cost * lapl.b + divf * mag * cost * div;
                float tb = feedf * uv.g + fluxf * flux.g - lapf * sint * lapl.b + divf * mag * sint * div;

                float a = ta * cos(curl_e) - tb * sin(curl_e);
                float b = ta * sin(curl_e) + tb * cos(curl_e);

                if(brush.x > 0.0)
                {
                    vec2 diff = (vUv - brush) / texel;
                    float dist = sqrt(dot(diff, diff));
                    if(dist < brushsize) {
                        a = diff.x / dist;
                        b = diff.y / dist;
                    }
                }
                
                gl_FragColor = vec4(clamp(a, -1.0, 1.0), clamp(b, -1.0, 1.0), div, 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="frustratedFlockingFluidFragmentShader">
            const float _K0 = -20.0/6.0; // center weight
            const float _K1 = 4.0/6.0; // edge-neighbors
            const float _K2 = 1.0/6.0; // vertex-neighbors
            const float sq2 = 0.7071068; // sqrt(2.0)/2.0;
            const float wds = 2.0 + 4.0 * sq2;
            const float nss = 4.0 + 2.0 * 1.41421356237;
            const float pi_over_2 = 1.57079632679;

            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform sampler2D tSource;
            uniform float divf;
            uniform float lapf;
            uniform float feedf;
            uniform float fluxf;
            uniform float flockf;
            uniform float frustf;
            uniform vec2 brush;
            uniform float brushsize;

            vec2 texel = vec2(1.0/screenWidth, 1.0/screenHeight);
            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;
            vec2 n  = vec2(0.0, step_y);
            vec2 ne = vec2(step_x, step_y);
            vec2 e  = vec2(step_x, 0.0);
            vec2 se = vec2(step_x, -step_y);
            vec2 s  = vec2(0.0, -step_y);
            vec2 sw = vec2(-step_x, -step_y);
            vec2 w  = vec2(-step_x, 0.0);
            vec2 nw = vec2(-step_x, step_y);

            float atan2(in float y, in float x)
            {
                float s = (abs(x) > abs(y)) ? 1.0 : 0.0;
                return mix(pi_over_2 - atan(x,y), atan(y,x), s);
            }

            void main()
            {
                vec3 uv = texture2D(tSource, vUv).rgb;

                vec3 uv_n = texture2D(tSource, vUv+n).rgb;
                vec3 uv_e = texture2D(tSource, vUv+e).rgb;
                vec3 uv_s = texture2D(tSource, vUv+s).rgb;
                vec3 uv_w = texture2D(tSource, vUv+w).rgb;

                vec3 uv_nw = texture2D(tSource, vUv+nw).rgb;
                vec3 uv_sw = texture2D(tSource, vUv+sw).rgb;
                vec3 uv_ne = texture2D(tSource, vUv+ne).rgb;
                vec3 uv_se = texture2D(tSource, vUv+se).rgb;

                vec3 lapl  = _K0*uv + _K1*(uv_n + uv_e + uv_w + uv_s) + _K2*(uv_nw + uv_sw + uv_ne + uv_se);

                float fa = (uv_w.r + uv_e.r + uv_n.r + uv_s.r + sq2 * (uv_nw.r + uv_sw.r + uv_ne.r + uv_se.r)) / (nss);
                float fb = (uv_w.g + uv_e.g + uv_n.g + uv_s.g + sq2 * (uv_nw.g + uv_sw.g + uv_ne.g + uv_se.g)) / (nss);

                vec2 flux  = vec2(uv_w.r + uv_e.r - wds * uv.r, uv_s.g + uv_n.g - wds * uv.g) + sq2 * (uv_nw.rg + uv_ne.rg + uv_se.rg + uv_sw.rg);
                float div  = (uv_s.g - uv_n.g - uv_e.r + uv_w.r + sq2 * (uv_nw.r - uv_nw.g) - sq2 * (uv_ne.r + uv_ne.g) + sq2 * (uv_sw.r + uv_sw.g) + sq2 * (uv_se.g - uv_se.r));

                float theta, theta2, cost, sint = 0.0;
                if (!(fa == 0.0 && fb == 0.0)) {
                    theta = atan2( fb, fa);
                    cost = cos(theta);
                    sint = sin(theta);
                }

                float ta = feedf * uv.r + fluxf * flux.r - lapf * cost * lapl.b + divf * cost * div;
                float tb = feedf * uv.g + fluxf * flux.g - lapf * sint * lapl.b + divf * sint * div;

                float a = ta * (1.0 - flockf) + fa * flockf - frustf * div * fa;
                float b = tb * (1.0 - flockf) + fb * flockf - frustf * div * fb;

                if(brush.x > 0.0)
                {
                    vec2 diff = (vUv - brush) / texel;
                    float dist = sqrt(dot(diff, diff));
                    if(dist < brushsize) {
                        a = diff.x / dist;
                        b = diff.y / dist;
                    }
                }

                gl_FragColor = vec4(clamp(a, -1.0, 1.0), clamp(b, -1.0, 1.0), clamp(div, -1.0, 1.0), 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="paintFragmentShader">

            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;

            uniform sampler2D tSource;
            uniform sampler2D tSource2;
            uniform sampler2D sSource;

            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;
            vec2 texel = vec2(step_x, step_y);
            vec2 n  = vec2(0.0, step_y);
            vec2 ne = vec2(step_x, step_y);
            vec2 e  = vec2(step_x, 0.0);
            vec2 se = vec2(step_x, -step_y);
            vec2 s  = vec2(0.0, -step_y);
            vec2 sw = vec2(-step_x, -step_y);
            vec2 w  = vec2(-step_x, 0.0);
            vec2 nw = vec2(-step_x, step_y);

            void main()
            {
                float u, v = 0.0;

                vec2 off0 = vec2(-step_x, -step_y);
                vec2 off1 = vec2( 0.0,    -step_y);
                vec2 off2 = vec2( step_x, -step_y);
                vec2 off3 = vec2(-step_x,  0.0);
                vec2 off4 = vec2( 0.0,     0.0);
                vec2 off5 = vec2( step_x,  0.0);
                vec2 off6 = vec2(-step_x,  step_y);
                vec2 off7 = vec2( 0.0,     step_y);
                vec2 off8 = vec2( step_x,  step_y);

                float gradX0 = (length(texture2D(tSource, vUv + w + off0).xy) - length(texture2D(tSource, vUv + e + off0).xy));
                float gradX1 = (length(texture2D(tSource, vUv + w + off1).xy) - length(texture2D(tSource, vUv + e + off1).xy));
                float gradX2 = (length(texture2D(tSource, vUv + w + off2).xy) - length(texture2D(tSource, vUv + e + off2).xy));
                float gradX3 = (length(texture2D(tSource, vUv + w + off3).xy) - length(texture2D(tSource, vUv + e + off3).xy));
                float gradX4 = (length(texture2D(tSource, vUv + w + off4).xy) - length(texture2D(tSource, vUv + e + off4).xy));
                float gradX5 = (length(texture2D(tSource, vUv + w + off5).xy) - length(texture2D(tSource, vUv + e + off5).xy));
                float gradX6 = (length(texture2D(tSource, vUv + w + off6).xy) - length(texture2D(tSource, vUv + e + off6).xy));
                float gradX7 = (length(texture2D(tSource, vUv + w + off7).xy) - length(texture2D(tSource, vUv + e + off7).xy));
                float gradX8 = (length(texture2D(tSource, vUv + w + off8).xy) - length(texture2D(tSource, vUv + e + off8).xy));

                float gradY0 = (length(texture2D(tSource, vUv + s + off0).xy) - length(texture2D(tSource, vUv + n + off0).xy));
                float gradY1 = (length(texture2D(tSource, vUv + s + off1).xy) - length(texture2D(tSource, vUv + n + off1).xy));
                float gradY2 = (length(texture2D(tSource, vUv + s + off2).xy) - length(texture2D(tSource, vUv + n + off2).xy));
                float gradY3 = (length(texture2D(tSource, vUv + s + off3).xy) - length(texture2D(tSource, vUv + n + off3).xy));
                float gradY4 = (length(texture2D(tSource, vUv + s + off4).xy) - length(texture2D(tSource, vUv + n + off4).xy));
                float gradY5 = (length(texture2D(tSource, vUv + s + off5).xy) - length(texture2D(tSource, vUv + n + off5).xy));
                float gradY6 = (length(texture2D(tSource, vUv + s + off6).xy) - length(texture2D(tSource, vUv + n + off6).xy));
                float gradY7 = (length(texture2D(tSource, vUv + s + off7).xy) - length(texture2D(tSource, vUv + n + off7).xy));
                float gradY8 = (length(texture2D(tSource, vUv + s + off8).xy) - length(texture2D(tSource, vUv + n + off8).xy));

                float gradT0 = length(texture2D(tSource2, vUv + off0).xy) - length(texture2D(tSource, vUv + off0).xy);
                float gradT1 = length(texture2D(tSource2, vUv + off1).xy) - length(texture2D(tSource, vUv + off1).xy);
                float gradT2 = length(texture2D(tSource2, vUv + off2).xy) - length(texture2D(tSource, vUv + off2).xy);
                float gradT3 = length(texture2D(tSource2, vUv + off3).xy) - length(texture2D(tSource, vUv + off3).xy);
                float gradT4 = length(texture2D(tSource2, vUv + off4).xy) - length(texture2D(tSource, vUv + off4).xy);
                float gradT5 = length(texture2D(tSource2, vUv + off5).xy) - length(texture2D(tSource, vUv + off5).xy);
                float gradT6 = length(texture2D(tSource2, vUv + off6).xy) - length(texture2D(tSource, vUv + off6).xy);
                float gradT7 = length(texture2D(tSource2, vUv + off7).xy) - length(texture2D(tSource, vUv + off7).xy);
                float gradT8 = length(texture2D(tSource2, vUv + off8).xy) - length(texture2D(tSource, vUv + off8).xy);

                float A2   = gradX0 * gradX0 + gradX1 * gradX1 + gradX2 * gradX2 + gradX3 * gradX3 + gradX4 * gradX4 + gradX5 * gradX5 + gradX6 * gradX6 + gradX7 * gradX7 + gradX8 * gradX8;
                float A1B2 = gradX0 * gradY0 + gradX1 * gradY1 + gradX2 * gradY2 + gradX3 * gradY3 + gradX4 * gradY4 + gradX5 * gradY5 + gradX6 * gradY6 + gradX7 * gradY7 + gradX8 * gradY8;
                float B1   = gradY0 * gradY0 + gradY1 * gradY1 + gradY2 * gradY2 + gradY3 * gradY3 + gradY4 * gradY4 + gradY5 * gradY5 + gradY6 * gradY6 + gradY7 * gradY7 + gradY8 * gradY8;
                float C2   = gradX0 * gradT0 + gradX1 * gradT1 + gradX2 * gradT2 + gradX3 * gradT3 + gradX4 * gradT4 + gradX5 * gradT5 + gradX6 * gradT6 + gradX7 * gradT7 + gradX8 * gradT8;
                float C1   = gradY0 * gradT0 + gradY1 * gradT1 + gradY2 * gradT2 + gradY3 * gradT3 + gradY4 * gradT4 + gradY5 * gradT5 + gradY6 * gradT6 + gradY7 * gradT7 + gradY8 * gradT8;

                float delta = (A1B2 * A1B2 - A2 * B1);

                if (delta != 0.0) {
                    /* system is not singular - solving by Kramer method */
                    float Idelta = 1.0/ delta;
                    float deltaX = -(C1 * A1B2 - C2 * B1);
                    float deltaY = -(A1B2 * C2 - A2 * C1);

                    u = deltaX * Idelta;
                    v = deltaY * Idelta;
                } else {
                    /* singular system - find optical flow in gradient direction */
                    float norm = (A1B2 + A2) * (A1B2 + A2) + (B1 + A1B2) * (B1 + A1B2);
                    if (norm != 0.0) {
                        float IGradNorm = 1.0/ norm;
                        float temp = -(C1 + C2) * IGradNorm;

                        u = (A1B2 + A2) * temp;
                        v = (B1 + A1B2) * temp;
                    } else {
                        u = v = 0.0;
                    }
                }

                float q = u * v;

                vec3 normed = 0.5 * normalize(vec3(u, v, q));
                vec3 offv = vec3(0.5);
                gl_FragColor = vec4(normed + offv, 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="screenFragmentShader">
            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform sampler2D tSource;
            uniform sampler2D sSource;
            
            void main()
            {
                gl_FragColor = texture2D(sSource, vUv);
            }
        </script>
        <script type="x-shader/x-fragment" id="debugScreenFragmentShader">
            varying vec2 vUv;
            uniform float screenWidth;
            uniform float screenHeight;
            uniform sampler2D tSource;
            uniform sampler2D sSource;

            void main()
            {
                vec3 uv = 0.5 + 0.5 * normalize(texture2D(tSource, vUv).rgb);
                gl_FragColor = vec4(uv, 1.0);
            }
        </script>
        <meta charset="UTF-8">
    </head>
    <body>
        <div id="simulation">
            <canvas id="myCanvas" class="viewer" style="width:1024px;height:1024px"></canvas>
        </div>
    </body>
</html>
