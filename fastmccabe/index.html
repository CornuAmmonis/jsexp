<!DOCTYPE html>
<html>
    <head>
        <title>Gaussian Pyramid McCabeism</title>
        <link rel="stylesheet" type="text/css" href="style.css"/>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r71/three.min.js"></script>
        <script type="text/javascript" src="../3rd/dat.gui.js"></script>
        <script type="text/javascript" src="../3rd/jquery-1.11.2.min.js"></script>
        <script type="text/javascript" src="render.js"></script>
        <script type="x-shader/x-vertex" id="standardVertexShader">
            varying vec2 vUv;

            void main()
            {
                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-fragment" id="standardScreenFragmentShader">
            varying vec2 vUv;
            uniform sampler2D iSource;

            void main()
            {
                vec4 is = (texture2D(iSource, vUv) + 1.0) / 2.0;
                gl_FragColor = vec4(is.xyz, 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="screenFragmentShader">
            varying vec2 vUv;
            uniform sampler2D iSource;
            uniform sampler2D bSource0;
            uniform sampler2D bSource1;
            uniform sampler2D bSource2;
            uniform sampler2D bSource3;

            void main()
            {
                /*
                vec4 is = texture2D(iSource, vUv);
                vec4 b0 = vec4(texture2D(bSource0, vUv).yw, texture2D(bSource1, vUv).yw);
                vec4 b1 = vec4(texture2D(bSource2, vUv).yw, texture2D(bSource3, vUv).yw);

                vec4 dog0 = vec4(is.x - b0.x, b0.x - b0.y, b0.y - b0.z, b0.z - b0.w);
                vec4 dog1 = vec4(b0.w - b1.x, b1.x - b1.y, b1.y - b1.z, b1.z - b1.w);

                float a = 1.0;
                vec4 a0 = vec4(2.0, 3.0, 4.0, 5.0);
                vec4 a1 = vec4(6.0, 7.0, 8.0, 9.0);

                float s = 0.2;
                float p = 0.2;

                a = s * pow(a, p);
                a0 = s * pow(a0, vec4(p));
                a1 = s * pow(a1, vec4(p));

                vec4 one = vec4(1.0);

                float final = dot(dog0 * a0, one) + dot(dog1 * a1, one);
                gl_FragColor = vec4(vec3(0.5 * (final + 1.0)), 1.0);
                */
                /*
                float b0 = texture2D(bSource0, vUv).y;
                float b1 = texture2D(bSource0, vUv).w;
                float b2 = texture2D(bSource1, vUv).y;
                float b3 = texture2D(bSource1, vUv).w;
                float a = texture2D(iSource, vUv).x;
                float m0 = mix(a,  b0, abs(a * a));
                float m1 = mix(m0, b1, abs(m0 * m0));
                float m2 = mix(m1, b2, abs(m1 * m1));
                float m3 = mix(m2, b3, abs(m2 * m2));
                vec4 is = (vec4(m3) + 1.0) * 0.5;
                gl_FragColor = vec4(is.xyz, 1.0);
                */
                vec4 is = (texture2D(iSource, vUv) + 1.0) * 0.5;
                gl_FragColor = vec4(is.xyz, 1.0);
            }
        </script>
        <script type="x-shader/x-fragment" id="noiseFragmentShader">
            varying vec2 vUv;

            float rand(vec2 co){
                return fract(sin(dot(co.xy ,vec2(12.9898,78.233))) * 43758.5453);
            }

            void main()
            {
                float is = rand(vUv);
                gl_FragColor = vec4(is);
            }
        </script>
        <script type="x-shader/x-vertex" id="gaussianPyramidVertexShader0">
            varying vec2 blurTexCoords0[8];
            varying vec2 blurTexCoords1[8];
            varying vec2 blurTexCoords2[4];
            varying vec2 blurTexCoords3[4];

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            void main()
            {
                float ix = 1.0 / screenWidth;
                float iy = 1.0 / screenHeight;

                blurTexCoords0[ 0] = uv + vec2(-ix * 4.0, 0.0);
                blurTexCoords0[ 1] = uv + vec2(-ix * 3.0, 0.0);
                blurTexCoords0[ 2] = uv + vec2(-ix * 2.0, 0.0);
                blurTexCoords0[ 3] = uv + vec2(-ix * 1.0, 0.0);
                blurTexCoords0[ 4] = uv + vec2( ix * 1.0, 0.0);
                blurTexCoords0[ 5] = uv + vec2( ix * 2.0, 0.0);
                blurTexCoords0[ 6] = uv + vec2( ix * 3.0, 0.0);
                blurTexCoords0[ 7] = uv + vec2( ix * 4.0, 0.0);

                blurTexCoords1[ 0] = uv + vec2(0.0, -iy * 4.0);
                blurTexCoords1[ 1] = uv + vec2(0.0, -iy * 3.0);
                blurTexCoords1[ 2] = uv + vec2(0.0, -iy * 2.0);
                blurTexCoords1[ 3] = uv + vec2(0.0, -iy * 1.0);
                blurTexCoords1[ 4] = uv + vec2(0.0,  iy * 1.0);
                blurTexCoords1[ 5] = uv + vec2(0.0,  iy * 2.0);
                blurTexCoords1[ 6] = uv + vec2(0.0,  iy * 3.0);
                blurTexCoords1[ 7] = uv + vec2(0.0,  iy * 4.0);

                blurTexCoords2[ 0] = uv + vec2(-ix * 8.0, 0.0);
                blurTexCoords2[ 1] = uv + vec2(-ix * 6.0, 0.0);
                blurTexCoords2[ 2] = uv + vec2( ix * 6.0, 0.0);
                blurTexCoords2[ 3] = uv + vec2( ix * 8.0, 0.0);

                blurTexCoords3[ 0] = uv + vec2(0.0, -iy * 8.0);
                blurTexCoords3[ 1] = uv + vec2(0.0, -iy * 6.0);
                blurTexCoords3[ 2] = uv + vec2(0.0,  iy * 6.0);
                blurTexCoords3[ 3] = uv + vec2(0.0,  iy * 8.0);

                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-vertex" id="gaussianPyramidVertexShader1">
            varying vec2 blurTexCoords0[8];
            varying vec2 blurTexCoords1[8];
            varying vec2 blurTexCoords2[4];
            varying vec2 blurTexCoords3[4];

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            void main()
            {
                float ix = 1.0 / screenWidth;
                float iy = 1.0 / screenHeight;

                blurTexCoords0[ 0] = uv + vec2(-ix * 16.0, 0.0);
                blurTexCoords0[ 1] = uv + vec2(-ix * 12.0, 0.0);
                blurTexCoords0[ 2] = uv + vec2(-ix *  8.0, 0.0);
                blurTexCoords0[ 3] = uv + vec2(-ix *  4.0, 0.0);
                blurTexCoords0[ 4] = uv + vec2( ix *  4.0, 0.0);
                blurTexCoords0[ 5] = uv + vec2( ix *  8.0, 0.0);
                blurTexCoords0[ 6] = uv + vec2( ix * 12.0, 0.0);
                blurTexCoords0[ 7] = uv + vec2( ix * 16.0, 0.0);

                blurTexCoords1[ 0] = uv + vec2(0.0, -iy * 16.0);
                blurTexCoords1[ 1] = uv + vec2(0.0, -iy * 12.0);
                blurTexCoords1[ 2] = uv + vec2(0.0, -iy *  8.0);
                blurTexCoords1[ 3] = uv + vec2(0.0, -iy *  4.0);
                blurTexCoords1[ 4] = uv + vec2(0.0,  iy *  4.0);
                blurTexCoords1[ 5] = uv + vec2(0.0,  iy *  8.0);
                blurTexCoords1[ 6] = uv + vec2(0.0,  iy * 12.0);
                blurTexCoords1[ 7] = uv + vec2(0.0,  iy * 16.0);

                blurTexCoords2[ 0] = uv + vec2(-ix * 32.0, 0.0);
                blurTexCoords2[ 1] = uv + vec2(-ix * 24.0, 0.0);
                blurTexCoords2[ 2] = uv + vec2( ix * 24.0, 0.0);
                blurTexCoords2[ 3] = uv + vec2( ix * 32.0, 0.0);

                blurTexCoords3[ 0] = uv + vec2(0.0, -iy * 32.0);
                blurTexCoords3[ 1] = uv + vec2(0.0, -iy * 24.0);
                blurTexCoords3[ 2] = uv + vec2(0.0,  iy * 24.0);
                blurTexCoords3[ 3] = uv + vec2(0.0,  iy * 32.0);

                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-vertex" id="gaussianPyramidVertexShader2">
            varying vec2 blurTexCoords0[8];
            varying vec2 blurTexCoords1[8];
            varying vec2 blurTexCoords2[4];
            varying vec2 blurTexCoords3[4];

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            void main()
            {
                float ix = 1.0 / screenWidth;
                float iy = 1.0 / screenHeight;

                blurTexCoords0[ 0] = uv + vec2(-ix * 64.0, 0.0);
                blurTexCoords0[ 1] = uv + vec2(-ix * 48.0, 0.0);
                blurTexCoords0[ 2] = uv + vec2(-ix * 32.0, 0.0);
                blurTexCoords0[ 3] = uv + vec2(-ix * 16.0, 0.0);
                blurTexCoords0[ 4] = uv + vec2( ix * 16.0, 0.0);
                blurTexCoords0[ 5] = uv + vec2( ix * 32.0, 0.0);
                blurTexCoords0[ 6] = uv + vec2( ix * 48.0, 0.0);
                blurTexCoords0[ 7] = uv + vec2( ix * 64.0, 0.0);

                blurTexCoords1[ 0] = uv + vec2(0.0, -iy * 64.0);
                blurTexCoords1[ 1] = uv + vec2(0.0, -iy * 48.0);
                blurTexCoords1[ 2] = uv + vec2(0.0, -iy * 32.0);
                blurTexCoords1[ 3] = uv + vec2(0.0, -iy * 16.0);
                blurTexCoords1[ 4] = uv + vec2(0.0,  iy * 16.0);
                blurTexCoords1[ 5] = uv + vec2(0.0,  iy * 32.0);
                blurTexCoords1[ 6] = uv + vec2(0.0,  iy * 48.0);
                blurTexCoords1[ 7] = uv + vec2(0.0,  iy * 64.0);

                blurTexCoords2[ 0] = uv + vec2(-ix * 128.0, 0.0);
                blurTexCoords2[ 1] = uv + vec2(-ix *  96.0, 0.0);
                blurTexCoords2[ 2] = uv + vec2( ix *  96.0, 0.0);
                blurTexCoords2[ 3] = uv + vec2( ix * 128.0, 0.0);

                blurTexCoords3[ 0] = uv + vec2(0.0, -iy * 128.0);
                blurTexCoords3[ 1] = uv + vec2(0.0, -iy *  96.0);
                blurTexCoords3[ 2] = uv + vec2(0.0,  iy *  96.0);
                blurTexCoords3[ 3] = uv + vec2(0.0,  iy * 128.0);

                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-vertex" id="gaussianPyramidVertexShader3">
            varying vec2 blurTexCoords0[8];
            varying vec2 blurTexCoords1[8];
            varying vec2 blurTexCoords2[4];
            varying vec2 blurTexCoords3[4];

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            void main()
            {
                float ix = 1.0 / screenWidth;
                float iy = 1.0 / screenHeight;

                blurTexCoords0[ 0] = uv + vec2(-ix * 256.0, 0.0);
                blurTexCoords0[ 1] = uv + vec2(-ix * 192.0, 0.0);
                blurTexCoords0[ 2] = uv + vec2(-ix * 128.0, 0.0);
                blurTexCoords0[ 3] = uv + vec2(-ix *  64.0, 0.0);
                blurTexCoords0[ 4] = uv + vec2( ix *  64.0, 0.0);
                blurTexCoords0[ 5] = uv + vec2( ix * 128.0, 0.0);
                blurTexCoords0[ 6] = uv + vec2( ix * 192.0, 0.0);
                blurTexCoords0[ 7] = uv + vec2( ix * 256.0, 0.0);

                blurTexCoords1[ 0] = uv + vec2(0.0, -iy * 256.0);
                blurTexCoords1[ 1] = uv + vec2(0.0, -iy * 192.0);
                blurTexCoords1[ 2] = uv + vec2(0.0, -iy * 128.0);
                blurTexCoords1[ 3] = uv + vec2(0.0, -iy *  64.0);
                blurTexCoords1[ 4] = uv + vec2(0.0,  iy *  64.0);
                blurTexCoords1[ 5] = uv + vec2(0.0,  iy * 128.0);
                blurTexCoords1[ 6] = uv + vec2(0.0,  iy * 192.0);
                blurTexCoords1[ 7] = uv + vec2(0.0,  iy * 256.0);

                blurTexCoords2[ 0] = uv + vec2(-ix * 512.0, 0.0);
                blurTexCoords2[ 1] = uv + vec2(-ix * 384.0, 0.0);
                blurTexCoords2[ 2] = uv + vec2( ix * 384.0, 0.0);
                blurTexCoords2[ 3] = uv + vec2( ix * 512.0, 0.0);

                blurTexCoords3[ 0] = uv + vec2(0.0, -iy * 512.0);
                blurTexCoords3[ 1] = uv + vec2(0.0, -iy * 384.0);
                blurTexCoords3[ 2] = uv + vec2(0.0,  iy * 384.0);
                blurTexCoords3[ 3] = uv + vec2(0.0,  iy * 512.0);

                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-vertex" id="gaussianPyramidFragmentShader0">
            uniform sampler2D iSource;
            uniform sampler2D sSource;

            varying vec2 blurTexCoords0[8];
            varying vec2 blurTexCoords1[8];
            varying vec2 blurTexCoords2[4];
            varying vec2 blurTexCoords3[4];

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            uniform float stddev;

            #define I0(i) dot(texture2D(iSource, blurTexCoords0[i]), vec4(1.0, 1.0, 1.0, 0.0))/3.0
            #define S0y(i) texture2D(sSource, blurTexCoords0[i]).y
            #define S2y(i) texture2D(sSource, blurTexCoords2[i]).y
            #define S1x(i) texture2D(sSource, blurTexCoords1[i]).x
            #define S1z(i) texture2D(sSource, blurTexCoords1[i]).z
            #define S3z(i) texture2D(sSource, blurTexCoords3[i]).z

            float gaussian(float x, float s) {
                return exp(-x*x/(s*s));
            }

            vec4 gaussian(vec4 x, float s) {
                return exp(-x*x/(s*s));
            }

            void main()
            {

                vec4 i = vec4(-4.0, -3.0, -2.0, -1.0);
                vec4 g0 = gaussian(i, stddev);
                float g = gaussian(0.0, stddev);
                float sum = 2.0 * (g0.x + g0.y + g0.z + g0.w) + g;
                g0 /= sum;
                g /= sum;

                float IC = dot(texture2D(iSource, vUv), vec4(1.0, 1.0, 1.0, 0.0))/3.0;
                vec3 SC = texture2D(sSource, vUv).xyz;

                // four 9-point 1D truncated gaussian convolutions
                vec4 f = vec4(0.0);
                f += (vec4(I0(0), S1x(0), S2y(0), S3z(0)) + vec4(I0(7), S1x(7), S2y(3), S3z(3))) * g0.x;
                f += (vec4(I0(1), S1x(1), S2y(1), S3z(1)) + vec4(I0(6), S1x(6), S2y(2), S3z(2))) * g0.y;
                f += (vec4(I0(2), S1x(2), S0y(0), S1z(0)) + vec4(I0(5), S1x(5), S0y(7), S1z(7))) * g0.z;
                f += (vec4(I0(3), S1x(3), S0y(2), S1z(2)) + vec4(I0(4), S1x(4), S0y(5), S1z(5))) * g0.w;
                f += vec4(IC, SC) * g;

                gl_FragColor = vec4(f);

            }
        </script>
        <script type="x-shader/x-vertex" id="gaussianPyramidFragmentShader123">
            uniform sampler2D iSource;
            uniform sampler2D sSource;

            varying vec2 blurTexCoords0[8];
            varying vec2 blurTexCoords1[8];
            varying vec2 blurTexCoords2[4];
            varying vec2 blurTexCoords3[4];

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            uniform float stddev;

            #define I0(i) texture2D(iSource, blurTexCoords0[i]).w
            #define S0y(i) texture2D(sSource, blurTexCoords0[i]).y
            #define S2y(i) texture2D(sSource, blurTexCoords2[i]).y
            #define S1x(i) texture2D(sSource, blurTexCoords1[i]).x
            #define S1z(i) texture2D(sSource, blurTexCoords1[i]).z
            #define S3z(i) texture2D(sSource, blurTexCoords3[i]).z

            float gaussian(float x, float s) {
                return exp(-x*x/(s*s));
            }

            vec4 gaussian(vec4 x, float s) {
                return exp(-x*x/(s*s));
            }

            void main()
            {

                vec4 i = vec4(-4.0, -3.0, -2.0, -1.0);
                vec4 g0 = gaussian(i, stddev);
                float g = gaussian(0.0, stddev);
                float sum = 2.0 * (g0.x + g0.y + g0.z + g0.w) + g;
                g0 /= sum;
                g /= sum;

                float IC = texture2D(iSource, vUv).w;
                vec3 SC = texture2D(sSource, vUv).xyz;

                // four 9-point 1D truncated gaussian convolutions
                vec4 f = vec4(0.0);
                f += (vec4(I0(0), S1x(0), S2y(0), S3z(0)) + vec4(I0(7), S1x(7), S2y(3), S3z(3))) * g0.x;
                f += (vec4(I0(1), S1x(1), S2y(1), S3z(1)) + vec4(I0(6), S1x(6), S2y(2), S3z(2))) * g0.y;
                f += (vec4(I0(2), S1x(2), S0y(0), S1z(0)) + vec4(I0(5), S1x(5), S0y(7), S1z(7))) * g0.z;
                f += (vec4(I0(3), S1x(3), S0y(2), S1z(2)) + vec4(I0(4), S1x(4), S0y(5), S1z(5))) * g0.w;
                f += vec4(IC, SC) * g;

                gl_FragColor = vec4(f);

            }
        </script>
        <script type="x-shader/x-fragment" id="mccabeFragmentShader">
            varying vec2 vUv;
            uniform sampler2D bSource0;
            uniform sampler2D bSource1;
            uniform sampler2D bSource2;
            uniform sampler2D bSource3;
            uniform sampler2D iSource;

            uniform float hard;
            uniform float sharp;
            uniform float exponent;
            uniform float scale;
            uniform float rate;
            uniform float hystj;
            uniform float hystk;
            uniform float feed;

            // clamp to avoid under/overflow, with a little headroom (highp range is 2^-62 to 2^62)
            vec4 underflowClamp(vec4 x) {
                return clamp(x, -42.9, 42.9);
            }

            float underflowClamp(float x) {
                return clamp(x, -42.9, 42.9);
            }

            vec4 expUC(vec4 x) {
                return exp(underflowClamp(x));
            }

            float expUC(float x) {
                return exp(underflowClamp(x));
            }

            /*  Wolfram CDF code for reference:
                Manipulate[ Plot3D[Coth[k]*(1/(1 + E^(j*x)) - 1/(1 + E^(j*x + k * y))),
                {x, -1, 1}, {y, -1, 1}, Mesh -> Automatic, MeshFunctions -> {#3 &},
                PlotRange -> {-1, 1}], {k, 0.001, 50.0}, {j, 0.001, 10.0}]
            */
            vec4 hysteresis(vec4 x, vec4 dx, float j, float k) {
                vec4 jx = j * x;
                vec4 exp_jx = 1.0 + expUC(jx);
                vec4 exp_jxkdx = 1.0 + expUC(jx + k * dx);
                float exp_pk = expUC(k);
                float exp_nk = expUC(-k);
                float cothk = (exp_pk + exp_nk)/(exp_pk - exp_nk);
                return (cothk / exp_jx) - (cothk / exp_jxkdx);
            }

            vec4 sigmoid(vec4 x, vec4 k) {
                return (2.0 / (1.0 + expUC(-k * x))) - 1.0;
            }

            void main()
            {
                vec4 one = vec4(1.0);

                vec4 c0 = vec4(0.365, 0.024, 0.259, 0.0);
                vec4 c1 = vec4(0.200, 0.306, 0.361, 0.0);
                vec4 c2 = vec4(0.271, 0.698, 0.616, 0.0);
                vec4 c3 = vec4(0.647, 0.780, 0.498, 0.0);
                vec4 c4 = vec4(0.937, 0.788, 0.298, 0.0);
                vec4 c5 = vec4(0.886, 0.478, 0.247, 0.0);
                vec4 c6 = vec4(0.875, 0.353, 0.286, 0.0);
                vec4 c7 = vec4(0.918, 0.376, 0.271, 0.0);

                c0 /= dot(c0, one);
                c1 /= dot(c1, one);
                c2 /= dot(c2, one);
                c3 /= dot(c3, one);
                c4 /= dot(c4, one);
                c5 /= dot(c5, one);
                c6 /= dot(c6, one);
                c7 /= dot(c7, one);

                vec4 i0 = vec4(1.0, 2.0, 3.0, 4.0);
                vec4 i1 = vec4(5.0, 6.0, 7.0, 8.0);

                vec4 g0 = vec4(0.0, 1.0, 1.0, 1.0);
                vec4 g1 = vec4(1.0, 1.0, 1.0, 1.0);

                vec4 ev = vec4(exponent);
                vec4 sv = vec4(sharp);

                // weights
                vec4 a0 = scale * pow(i0, ev);
                vec4 a1 = scale * pow(i1, ev);

                vec4 b0 = vec4(texture2D(bSource0, vUv).yw, texture2D(bSource1, vUv).yw);
                vec4 b1 = vec4(texture2D(bSource2, vUv).yw, texture2D(bSource3, vUv).yw);
                vec4 iv = texture2D(iSource, vUv);
                float is = dot(iv, vec4(1.0, 1.0, 1.0, 0.0)) / 3.0;
                float il = length(iv);

                // difference of gaussians
                vec4 dog0 = vec4(is   - b0.x, b0.x - b0.y, b0.y - b0.z, b0.z - b0.w);
                vec4 dog1 = vec4(b0.w - b1.x, b1.x - b1.y, b1.y - b1.z, b1.z - b1.w);

                // combined softmin and mccabe update rule
                vec4 q0 = g0 * expUC(- 10.0 * hard * abs(dog0));
                vec4 q1 = g1 * expUC(- 10.0 * hard * abs(dog1));
                vec4 n0 = sigmoid(dog0, sv) * a0 * q0;
                vec4 n1 = sigmoid(dog1, sv) * a1 * q1;
                float n = dot(n0, one) + dot(n1, one);
                float d = dot(q0, one) + dot(q1, one);
                vec4 f0 = n0 / d;
                vec4 f1 = n1 / d;

                vec4 cf = f0.x * c0 + f0.y * c1 + f0.z * c2 + f0.w * c3
                        + f1.x * c4 + f1.y * c5 + f1.z * c6 + f1.w * c7;

                vec4 dx = rate * hysteresis(iv, cf, hystj, hystk);

                vec4 to = vec4(2.0, 2.0, 2.0, 0.0);
                vec4 final = clamp(iv + dx - feed * is * is * is * is * (is * to - iv) , -1.0, 1.0);
                gl_FragColor = final;

            }
        </script>
        <script type="x-shader/x-fragment" id="debugFragmentShader">
            varying vec2 vUv;
            uniform sampler2D bSource0;
            uniform sampler2D bSource1;
            uniform sampler2D bSource2;
            uniform sampler2D bSource3;
            uniform sampler2D iSource;

            uniform float blurl;

            void main()
            {
                vec4 one = vec4(1.0);
                vec4 off = vec4(0.1);

                vec4 b0 = vec4(texture2D(bSource0, vUv).yw, texture2D(bSource1, vUv).yw);
                vec4 b1 = vec4(texture2D(bSource2, vUv).yw, texture2D(bSource3, vUv).yw);
                vec4 is = texture2D(iSource, vUv);

                float bb = blurl * blurl;
                vec4 d0 = vec4(bb, 1.0 - 2.0 * blurl + bb, 4.0 - 4.0 * blurl + bb, 9.0 - 6.0 * blurl + bb);
                vec4 d1 = vec4(16.0 - 8.0 * blurl + bb, 25.0 - 10.0 * blurl + bb, 36.0 - 12.0 * blurl + bb, 49.0 - 14.0 * blurl + bb);

                float n = dot(b0  / (off + d0), one) + dot(b1  / (off + d1), one);
                float d = dot(one / (off + d0), one) + dot(one / (off + d1), one);

                gl_FragColor = vec4(n / d);
            }
        </script>
        <meta charset="UTF-8">
    </head>
    <body>
        <div id="simulation">
            <canvas id="myCanvas" class="viewer" style="width:1024px;height:1024px"></canvas>
        </div>
    </body>
</html>
