<!DOCTYPE html>
<html>
    <head>
        <title>Softmax Manifold Denoise</title>
        <link rel="stylesheet" type="text/css" href="softmax.css"/>
        <script type="text/javascript" src="https://cdnjs.cloudflare.com/ajax/libs/three.js/r71/three.min.js"></script>
        <script type="text/javascript" src="../3rd/dat.gui.js"></script>
        <script type="text/javascript" src="../3rd/jquery-1.11.2.min.js"></script>
        <script type="text/javascript" src="softmax.js"></script>
        <script type="x-shader/x-vertex" id="standardVertexShader">
            varying vec2 vUv;

            void main()
            {
                vUv = uv;
                gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
            }
        </script>
        <script type="x-shader/x-fragment" id="screenFragmentShader">
            varying vec2 vUv;
            uniform sampler2D sSource;
            uniform sampler2D iSource;

            uniform float detail;

            void main()
            {
                vec4 is = texture2D(iSource, vUv);
                vec4 ss = texture2D(sSource, vUv);
                gl_FragColor = detail * (is - ss) + ss;
            }
        </script>
        <script type="x-shader/x-fragment" id="pathTracerFragmentShader">
            // Copied almost verbatim from here:
            // https://www.shadertoy.com/view/4sfGDB


            /*

             This shader is an attempt at porting smallpt to GLSL.

             See what it's all about here:
             http://www.kevinbeason.com/smallpt/

             The code is based in particular on the slides by David Cline.

             Some differences:

             - For optimization purposes, the code considers there is
             only one light source (see the commented loop)
             - Russian roulette and tent filter are not implemented

             I spent quite some time pulling my hair over inconsistent
             behavior between Chrome and Firefox, Angle and native. I
             expect many GLSL related bugs to be lurking, on top of
             implementation errors. Please Let me know if you find any.

             --
             Zavie

             */

            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            uniform int time;

            // Play with the two following values to change quality.
            // You want as many samples as your GPU can bear. :)
            #define SAMPLES 6
            #define MAXDEPTH 4

            // Uncomment to see how many samples never reach a light source
            //#define DEBUG

            // Not used for now
            #define DEPTH_RUSSIAN 2

            #define PI 3.14159265359
            #define DIFF 0
            #define SPEC 1
            #define REFR 2
            #define NUM_SPHERES 9

            float seed = 0.;
            float rand() { return fract(sin(seed++)*43758.5453123); }

            struct Ray { vec3 o, d; };
            struct Sphere {
                float r;
                vec3 p, e, c;
                int refl;
            };

            Sphere lightSourceVolume = Sphere(20., vec3(50., 81.6, 81.6), vec3(12.), vec3(0.), DIFF);
            Sphere spheres[NUM_SPHERES];
            void initSpheres() {
                spheres[0] = Sphere(1e5, vec3(-1e5+1., 40.8, 81.6),	vec3(0.), vec3(.75, .25, .25), DIFF);
                spheres[1] = Sphere(1e5, vec3( 1e5+99., 40.8, 81.6),vec3(0.), vec3(.25, .25, .75), DIFF);
                spheres[2] = Sphere(1e5, vec3(50., 40.8, -1e5),		vec3(0.), vec3(.75), DIFF);
                spheres[3] = Sphere(1e5, vec3(50., 40.8,  1e5+170.),vec3(0.), vec3(0.), DIFF);
                spheres[4] = Sphere(1e5, vec3(50., -1e5, 81.6),		vec3(0.), vec3(.75), DIFF);
                spheres[5] = Sphere(1e5, vec3(50.,  1e5+81.6, 81.6),vec3(0.), vec3(.75), DIFF);
                spheres[6] = Sphere(16.5, vec3(27., 16.5, 47.), 	vec3(0.), vec3(1.), SPEC);
                spheres[7] = Sphere(16.5, vec3(73., 16.5, 78.), 	vec3(0.), vec3(.7, 1., .9), REFR);
                spheres[8] = Sphere(600., vec3(50., 681.33, 81.6),	vec3(12.), vec3(0.), DIFF);
            }

            float intersect(Sphere s, Ray r) {
                vec3 op = s.p - r.o;
                float t, epsilon = 1e-3, b = dot(op, r.d), det = b * b - dot(op, op) + s.r * s.r;
                if (det < 0.) return 0.; else det = sqrt(det);
                return (t = b - det) > epsilon ? t : ((t = b + det) > epsilon ? t : 0.);
            }

            int intersect(Ray r, out float t, out Sphere s, int avoid) {
                int id = -1;
                t = 1e5;
                s = spheres[0];
                for (int i = 0; i < NUM_SPHERES; ++i) {
                    Sphere S = spheres[i];
                    float d = intersect(S, r);
                    if (i!=avoid && d!=0. && d<t) { t = d; id = i; s=S; }
                }
                return id;
            }

            vec3 jitter(vec3 d, float phi, float sina, float cosa) {
                vec3 w = normalize(d), u = normalize(cross(w.yzx, w)), v = cross(w, u);
                return (u*cos(phi) + v*sin(phi)) * sina + w * cosa;
            }

            vec3 radiance(Ray r) {
                vec3 acc = vec3(0.);
                vec3 mask = vec3(1.);
                int id = -1;
                for (int depth = 0; depth < MAXDEPTH; ++depth) {
                    float t;
                    Sphere obj;
                    if ((id = intersect(r, t, obj, id)) < 0) break;
                    vec3 x = t * r.d + r.o;
                    vec3 n = normalize(x - obj.p), nl = n * sign(-dot(n, r.d));

                    //vec3 f = obj.c;
                    //float p = dot(f, vec3(1.2126, 0.7152, 0.0722));
                    //if (depth > DEPTH_RUSSIAN || p == 0.) if (rand() < p) f /= p; else { acc += mask * obj.e * E; break; }

                    if (obj.refl == DIFF) {
                        float r2 = rand();
                        vec3 d = jitter(nl, 2.*PI*rand(), sqrt(r2), sqrt(1. - r2));
                        vec3 e = vec3(0.);
                        //for (int i = 0; i < NUM_SPHERES; ++i)
                        {
                            // Sphere s = sphere(i);
                            // if (dot(s.e, vec3(1.)) == 0.) continue;

                            // Normally we would loop over the light sources and
                            // cast rays toward them, but since there is only one
                            // light source, that is mostly occluded, here goes
                            // the ad hoc optimization:
                            Sphere s = lightSourceVolume;
                            int i = 8;

                            vec3 l0 = s.p - x;
                            float cos_a_max = sqrt(1. - clamp(s.r * s.r / dot(l0, l0), 0., 1.));
                            float cosa = mix(cos_a_max, 1., rand());
                            vec3 l = jitter(l0, 2.*PI*rand(), sqrt(1. - cosa*cosa), cosa);

                            if (intersect(Ray(x, l), t, s, id) == i) {
                                float omega = 2. * PI * (1. - cos_a_max);
                                e += (s.e * clamp(dot(l, n),0.,1.) * omega) / PI;
                            }
                        }
                        float E = 1.;//float(depth==0);
                        acc += mask * obj.e * E + mask * obj.c * e;
                        mask *= obj.c;
                        r = Ray(x, d);
                    } else if (obj.refl == SPEC) {
                        acc += mask * obj.e;
                        mask *= obj.c;
                        r = Ray(x, reflect(r.d, n));
                    } else {
                        float a=dot(n,r.d), ddn=abs(a);
                        float nc=1., nt=1.5, nnt=mix(nc/nt, nt/nc, float(a>0.));
                        float cos2t=1.-nnt*nnt*(1.-ddn*ddn);
                        r = Ray(x, reflect(r.d, n));
                        if (cos2t>0.) {
                            vec3 tdir = normalize(r.d*nnt + sign(a)*n*(ddn*nnt+sqrt(cos2t)));
                            float R0=(nt-nc)*(nt-nc)/((nt+nc)*(nt+nc)),
                                    c = 1.-mix(ddn,dot(tdir, n),float(a>0.));
                            float Re=R0+(1.-R0)*c*c*c*c*c,P=.25+.5*Re,RP=Re/P,TP=(1.-Re)/(1.-P);
                            if (rand()<P) { mask *= RP; }
                            else { mask *= obj.c*TP; r = Ray(x, tdir); }
                        }
                    }
                }
                return acc;
            }

            void main() {
                initSpheres();
                seed = float(time) * 0.0001 + 0.001 * (screenWidth * screenHeight * vUv.y + screenWidth * vUv.x);
                vec2 uv = 2. * vUv.xy - 1.;
                vec3 camPos = vec3(50., 40.8, 169.);
                vec3 cz = normalize(vec3(50., 40., 81.6) - camPos);
                vec3 cx = vec3(1., 0., 0.);
                vec3 cy = normalize(cross(cx, cz)); cx = cross(cz, cy);
                vec3 color = vec3(0.);
                for (int i = 0; i < SAMPLES; ++i)
                {
                #ifdef DEBUG
                    vec3 test = radiance(Ray(camPos, normalize(.53135 * (screenWidth / screenHeight * uv.x * cx + uv.y * cy) + cz)));
                    if (dot(test, test) > 0.) color += vec3(1.); else color += vec3(0.5,0.,0.1);
                #else
                    color += radiance(Ray(camPos, normalize(.53135 * (screenWidth / screenHeight * uv.x * cx + uv.y * cy) + cz)));
                #endif
                }
                gl_FragColor = vec4(pow(clamp(color/float(SAMPLES), 0., 1.), vec3(1./2.2)), 1.);
            }
        </script>
        <script type="x-shader/x-vertex" id="feedbackFragmentShader">
            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            uniform sampler2D sSource;
            uniform sampler2D iSource;

            uniform int step;
            uniform int chan;
            uniform float amount;

            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;

            const int radius = 6;

            vec3 getX(sampler2D src, int ch, float j, float s_x) {
                if (ch == 0) {
                    return vec3(texture2D(src, vUv + j * vec2(s_x, 0.0)).x);
                } else if (ch == 1) {
                    return vec3(texture2D(src, vUv + j * vec2(s_x, 0.0)).y);
                } else {
                    return vec3(texture2D(src, vUv + j * vec2(s_x, 0.0)).z);
                }
            }

            vec3 minmax_transformX(sampler2D src, float scale, int ch) {
                vec3 n = vec3(0.0);
                vec3 d = vec3(0.0);
                for (int j = -radius; j <= radius; j++) {
                    vec3 x = getX(src, ch, float(j), step_x);
                    vec3 v = vec3(- scale, 0.0, scale);
                    vec3 q = exp(v * x);
                    n += x * q;
                    d += q;
                }
                return n / d;
            }

            vec3 minmax_transformY(sampler2D src, float scale) {
                vec3 n = vec3(0.0);
                vec3 d = vec3(0.0);
                for (int j = -radius; j <= radius; j++) {
                    vec3 x = texture2D(src, vUv + float(j) * vec2(0.0, step_y)).xyz;
                    vec3 v = vec3(- scale, 0.0, scale);
                    vec3 q = exp(v * x);
                    n += x * q;
                    d += q;
                }
                return n / d;
            }

            void main()
            {
                vec3 result;
                if (step == 0) {
                    result = minmax_transformX(iSource, amount, chan);
                } else {
                    result = minmax_transformY(sSource, amount);
                }

                gl_FragColor = vec4(result, 1.0);
            }
        </script>
        <script type="x-shader/x-vertex" id="sliceFragmentShader">
            varying vec2 vUv;

            uniform float screenWidth;
            uniform float screenHeight;

            uniform sampler2D sSource0, sSource1, sSource2;
            uniform sampler2D iSource;

            uniform float exps;
            uniform float mix;

            float step_x = 1.0/screenWidth;
            float step_y = 1.0/screenHeight;

            const int radius = 6;

            float getI(int ch) {
                if (ch == 0) {
                    return texture2D(iSource, vUv).x;
                } else if (ch == 1) {
                    return texture2D(iSource, vUv).y;
                } else {
                    return texture2D(iSource, vUv).z;
                }
            }

            float slice(sampler2D sSrc, int ch) {
                vec3 x = texture2D(sSrc, vUv).xyz;
                float i = getI(ch);
                vec3 d = exp(- 2.0 * exps * abs(i - x));
                vec3 dx = d * x;
                float denom = (d.x + d.y + d.z);
                float weighted = 0.0;
                if (denom <= 0.0) {
                    weighted = x.y;
                } else {
                    weighted = (dx.x + dx.y + dx.z) / denom;
                }
                return mix * weighted + (1.0 - mix) * i;
            }

            void main()
            {
                vec3 result = vec3(
                    slice(sSource0, 0),
                    slice(sSource1, 1),
                    slice(sSource2, 2)
                );
                gl_FragColor = vec4(result, 1.0);
            }
        </script>
        <script type="x-shader/x-vertex" id="accumulatorFragmentShader">
            varying vec2 vUv;

            uniform sampler2D iSource;
            uniform sampler2D sSource;
            uniform float arate;

            void main()
            {
                vec3 a = texture2D(iSource, vUv).xyz;
                vec3 s = texture2D(sSource, vUv).xyz;
                gl_FragColor = vec4(a + arate * (s - a), 1.0);
            }
        </script>
        <meta charset="UTF-8">
    </head>
    <body>
        <div id="simulation">
            <canvas id="myCanvas" class="viewer" style="width:1024px;height:1024px"></canvas>
        </div>
    </body>
</html>
